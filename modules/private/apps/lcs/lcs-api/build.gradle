task deployDependencies(type: Copy)

dependencies {
	provided group: "com.fasterxml.jackson.core", name: "jackson-annotations", version: "2.4.2"
	provided group: "com.fasterxml.jackson.core", name: "jackson-core", version: "2.4.2"
	provided group: "com.fasterxml.jackson.core", name: "jackson-databind", version: "2.4.2"
	provided group: "com.liferay", name: "com.liferay.petra.json.web.service.client", version: "1.3.0"
	provided group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "2.0.0"
	provided group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
    provided group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"
    provided group: "org.slf4j", name: "slf4j-api", version: "1.7.2"
}

deploy {
	finalizedBy deployDependencies
}

deployDependencies {
	boolean keepDependencyVersions = Boolean.getBoolean("deploy.dependencies.keep.versions")

	ext {
		autoClean = false
	}

	from configurations.provided

	include "com.liferay.petra.json.web.service.client-*.jar"
	include "jackson-annotations-*.jar"
	include "jackson-core-*.jar"
	include "jackson-databind-*.jar"

	into {
		liferay.deployDir
	}

	outputs.upToDateWhen {
		false
	}

	String renameSuffix = ".jar"

	if (keepDependencyVersions) {
		renameSuffix = '-$1.jar'
	}

	rename(/com.liferay.petra.json.web.service.client-(.+)\.jar/, "com.liferay.petra.json.web.service.client" + renameSuffix)
	rename(/jackson-annotations-(.+)\.jar/, "com.fasterxml.jackson.core.jackson.annotations" + renameSuffix)
	rename(/jackson-core-(.+)\.jar/, "com.fasterxml.jackson.core.jackson.core" + renameSuffix)
	rename(/jackson-databind-(.+)\.jar/, "com.fasterxml.jackson.core.jackson.databind" + renameSuffix)
}