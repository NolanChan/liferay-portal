import com.liferay.gradle.util.GradleUtil

task deployConfigs(type: Copy)

dependencies {
	provided group: "biz.aQute.bnd", name: "biz.aQute.bndlib", version: "3.1.0"
	provided group: "com.liferay", name: "com.liferay.lcs.api", version: "5.0.0"
	provided group: "com.liferay", name: "com.liferay.osgi.util", version: "3.0.0"
	provided group: "com.liferay", name: "com.liferay.portal.configuration.metatype", version: "2.0.0"
	provided group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "2.0.0"
	provided group: "org.osgi", name: "org.osgi.core", version: "5.0.0"
}

deploy {
	finalizedBy deployConfigs
}

deployConfigs {
	String applicationProfile = GradleUtil.getProperty(project, "applicationProfile", "")

	ext {
		autoClean = false
	}

	from "configs/com.liferay.osb.lcs.configuration.OSBLCSConfiguration.cfg"

	if (applicationProfile.equals("LOCAL_DEVELOPMENT")) {
		from "configs/com.liferay.osb.lcs.configuration.OSBLCSDevelopmentConfiguration.cfg"
	}
	else if (applicationProfile.equals("PRODUCTION")) {
		from "configs/com.liferay.osb.lcs.configuration.OSBLCSProductionConfiguration.cfg"
	}
	else if (applicationProfile.equals("QA")) {
		from "configs/com.liferay.osb.lcs.configuration.OSBLCSQAConfiguration.cfg"
	}
	else if (applicationProfile.equals("TEST")) {
		from "configs/com.liferay.osb.lcs.configuration.OSBLCSTestConfiguration.cfg"
	}

	into new File(liferay.liferayHome, "osgi/configs")
}