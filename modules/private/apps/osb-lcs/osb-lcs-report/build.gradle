task deployDependencies(type: Copy)

dependencies {
	provided group: "com.itextpdf", name: "itextpdf", version: "5.5.5"
	provided group: "com.liferay", name: "com.liferay.lcs.api", version: "5.0.0"
	provided group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "2.0.0"
	provided group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	provided group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"
	provided project(":private:apps:osb-lcs:osb-lcs-api")
}

deploy {
	finalizedBy deployDependencies
}

deployDependencies {
	boolean keepDependencyVersions = Boolean.getBoolean("deploy.dependencies.keep.versions")

	ext {
		autoClean = false
	}

	from configurations.provided
	include "itextpdf-*.jar"

	into {
		liferay.deployDir
	}

	outputs.upToDateWhen {
		false
	}

	String renameSuffix = ".jar"

	if (keepDependencyVersions) {
		renameSuffix = '-$1.jar'
	}

	rename(/itextpdf-(.+)\.jar/, "com.itextpdf" + renameSuffix)
}