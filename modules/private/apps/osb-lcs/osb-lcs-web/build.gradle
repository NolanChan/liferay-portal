task deployDependencies(type: Copy)

dependencies {
	provided group: "com.liferay", name: "com.liferay.lcs.api", version: "4.1.0"
	provided group: "com.liferay", name: "com.liferay.portal.configuration.metatype", version: "2.0.0"
	provided group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "2.0.0"
	provided group: "com.liferay.portal", name: "com.liferay.util.taglib", version: "2.0.0"
	provided group: "commons-configuration", name: "commons-configuration", version: "1.10"
	provided group: "commons-fileupload", name: "commons-fileupload", version: "1.3.1"
	provided group: "commons-io", name: "commons-io", version: "2.4"
	provided group: "javax.portlet", name: "portlet-api", version: "2.0"
	provided group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	provided group: "javax.servlet.jsp", name: "javax.servlet.jsp-api", version: "2.3.1"
	provided group: "joda-time", name: "joda-time", version: "2.9.4"
	provided group: "org.apache.httpcomponents", name: "httpclient", version: "4.3.3"
	provided group: "org.apache.httpcomponents", name: "httpcore", version: "4.3.2"
	provided group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"
	provided project(":private:apps:osb-lcs-nosql:osb-lcs-nosql-api")
	provided project(":private:apps:osb-lcs:osb-lcs-api")
	provided project(":private:apps:osb-lcs:osb-lcs-report")
	provided project(":private:apps:osb-lcs:osb-lcs-service")
}

deploy {
	finalizedBy deployDependencies
}

deployDependencies {
	boolean keepDependencyVersions = Boolean.getBoolean("deploy.dependencies.keep.versions")

	ext {
		autoClean = false
	}

	from configurations.provided

	include "commons-configuration-*.jar"
	include "httpclient-*.jar"
	include "httpcore-*.jar"
	include "joda-time-*.jar"

	into {
		liferay.deployDir
	}

	outputs.upToDateWhen {
		false
	}

	String renameSuffix = ".jar"

	if (keepDependencyVersions) {
		renameSuffix = '-$1.jar'
	}

	rename(/commons-configuration-(.+)\.jar/, "org.apache.commons.configuration" + renameSuffix)
	rename(/httpclient-(.+)\.jar/, "org.apache.httpcomponents.httpclient" + renameSuffix)
	rename(/httpcore-(.+)\.jar/, "org.apache.httpcomponents.httpcore" + renameSuffix)
	rename(/joda-time-(.+)\.jar/, "org.joda.time" + renameSuffix)
}