configurations {
	embedCompile {
		exclude group: "log4j"
		exclude group: "org.apache.servicemix.bundles"
	}
}

dependencies {
	embedCompile group: "org.apache.directory.api", name: "apache-ldap-api", version: "1.0.0-M31"
	embedCompile group: "org.samba.jcifs", name: "jcifs", version: "1.3.14-kohsuke-1"
	embedCompile group: "pull-parser", name: "pull-parser", version: "2.1.10"
	embedCompile group: "xmlpull", name: "xmlpull", version: "1.1.3.1"

	compile configurations.embedCompile

	provided group: "biz.aQute.bnd", name: "biz.aQute.bndlib", version: "3.1.0"
	provided group: "com.liferay", name: "com.liferay.portal.instance.lifecycle", version: "3.0.0"
	provided group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "2.4.0"
	provided group: "org.osgi", name: "org.osgi.core", version: "5.0.0"
}

task syncThirdPartyLibs(type: Sync) {
	into "lib"
	from configurations.embedCompile
}

jar.dependsOn(syncThirdPartyLibs)

def bundleClassPath() {
    def classPath = ["."] + configurations.embedCompile.collect {
        "OSGI-INF/lib/" + it.name
    }

    return classPath.join(",")
}

bundle {
  instructions << [
    "Bundle-ClassPath": bundleClassPath()
  ]
}